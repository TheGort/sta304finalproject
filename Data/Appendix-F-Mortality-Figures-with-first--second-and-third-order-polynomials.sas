
%macro pull_cy(file,group,suf);
data junk2;
   set &file.;
  *Create month variable I will need for means procedure;
   months_to_21_yr = (30*floor((365*days_to_21_yr)/30)+15)/365;
  *Create pseudo weeks;  
   weeks_to_21_yr = (7*floor((365*days_to_21_yr)/7)+3.5)/365;
  *Take care of the columns that have been wacked;
   &group._&suf._red = exp(&group._&suf._red_l);
   if &group._&suf._red_l = 0.0001 then &group._&suf._red = 0;
   cubic = square*linear;
   cubic_post = square_post*linear_post;
run;
*Run the regression for the linear;
proc reg data = junk2 outseb outest = junk3 (where=(_TYPE_='PARMS'))  ;
   model &group._&suf._red = post linear  linear_post ;
   title "&group.";
run;
*This will read the regression output into a macro;
data _null_;
   set junk3;
   call symput('Intercept1',Intercept);
   call symput('post1',post);
   call symput('linear1',linear);
   call symput('linear_post1',linear_post);
run;
*Run the regression for the quadratic;
proc reg data = junk2 outseb outest = junk4 (where=(_TYPE_='PARMS'))  ;
   model &group._&suf._red = post linear square linear_post square_post;
   title "&group.";
run;
*This will read the regression output into a macro;
data _null_;
   set junk4;
   call symput('Intercept',Intercept);
   call symput('post',post);
   call symput('linear',linear);
   call symput('square',square);
   call symput('linear_post',linear_post);
   call symput('square_post',square_post);
run;
*Run the regression for the cubic;
proc reg data = junk2 outseb outest = junk5 (where=(_TYPE_='PARMS'))  ;
   model &group._&suf._red = post linear square cubic linear_post square_post cubic_post;
   title "&group.";
run;
*This will read the regression output into a macro;
data _null_;
   set junk5;
   call symput('Intercept3',Intercept);
   call symput('post3',post);
   call symput('linear3',linear);
   call symput('square3',square);
   call symput('cubic3',cubic);
   call symput('linear_post3',linear_post);
   call symput('square_post3',square_post);
   call symput('cubic_post3',cubic_post);
run;
*Compute the means by month cells;
proc means data = junk2 noprint;
   var &group._&suf._red;
   class months_to_21_yr;
   output out = junk6 (where =(_stat_ = 'MEAN' and _type_ = 1));
run;
*These are the group lists;
data &group._&suf. (drop =  _TYPE_  _stat_ _freq_
                     post linear square linear_post square_post );
   set junk6;
   if months_to_21_yr >= 0 then post = 1; else post = 0;
   linear = months_to_21_yr;
   square = linear*linear;
   cubic = linear*square;
   linear_post = linear*post;
   square_post = square*post;
   cubic_post = cubic*post;

   &group._&suf._ft_2 = &Intercept. + &post.*post + &linear.*linear + &square.*square + &linear_post.*linear_post + &square_post.*square_post;
  *Linear fit;
   &group._&suf._ft_1 = &Intercept1. + &post1.*post + &linear1.*linear + &linear_post1.*linear_post;
  *Cubic fit;
    &group._&suf._ft_3 = &Intercept3. + &post3.*post + &linear3.*linear + &square3.*square + &cubic3.*cubic + &linear_post3.*linear_post + &square_post3.*square_post + &cubic_post3.*cubic_post;
run;
proc datasets nolist;
   delete junk1 junk2 junk3 junk4 junk5 junk6;
run;
%mend;
%let file = ;
%pull_cy(for_stata_mort,all_cause,all);
%pull_cy(for_stata_mort,internal,all);
%pull_cy(for_stata_mort,external,all);

*Combine the files;
data AppendixG;
   merge all_cause_all internal_all external_all;
    by months_to_21_yr;
 run;
*Export;
PROC EXPORT DATA= AppendixG
            OUTFILE= "C:\Documents and Settings\RDC3\Desktop\Dobkin\Alcohol 21 for DC March 2007\Appendix G Mortality Figures with first, second and third order polynomials.csv" 
            DBMS=csv REPLACE;
RUN;

